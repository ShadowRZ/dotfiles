### SPDX-License-Identifier: MIT -*- Sh -*-

# Don't run ~/.zshrc in non interactive shell.
[[ $- != *i* ]] && return

# Start zprof.
(( $+NAVIFUTABA_ENABLE_ZPROF )) && zmodload zsh/zprof

# Zinit module.
module_path+=( "$HOME/.zinit/bin/zmodules/Src" )
zmodload zdharma/zplugin &> /dev/null
# Set up $_zdir.
_zdir="${ZDOTDIR:-$HOME}/.zsh"

# Options

# History related options.
setopt HIST_VERIFY
setopt INC_APPEND_HISTORY
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_FIND_NO_DUPS
setopt HIST_REDUCE_BLANKS
setopt EXTENDED_HISTORY
setopt HIST_FCNTL_LOCK
setopt HIST_IGNORE_SPACE
# Completion related options.
setopt ALWAYS_TO_END
setopt AUTO_MENU
unsetopt MENU_COMPLETE
# Navigation related options.
setopt PUSHD_MINUS
setopt AUTO_CD
setopt AUTO_PUSHD
# Globbing related options.
setopt EXTENDED_GLOB
setopt NOMATCH
# I/O related options.
setopt NO_CLOBBER
setopt INTERACTIVE_COMMENTS
setopt RC_QUOTES
setopt CORRECT
unsetopt FLOW_CONTROL
# Remove any RPROMPT after executing command.
setopt TRANSIENT_RPROMPT
# Prompt.
setopt PROMPT_SUBST

autoload -Uz colors
colors

# Shell used paramters.
HISTFILE=~/.zsh_history
HISTSIZE=35000
SAVEHIST="$HISTSIZE"
# Spelling correct prompt.
SPROMPT="%B%F{yellow}[Correct]%f%b %B%F{red}%R%f%b -> %B%F{green}%r%f%b ? [%B(N)%b/y/a/e]"
REPORTTIME=5
CORRECT_IGNORE='_*'
# Time format.
() {
    local white_b=$fg_bold[white] yellow_b=$fg_bold[yellow] blue=$fg[blue] rst=$reset_color
    TIMEFMT=(
        "${yellow_b}EST=%*Es$rst $white_b%J$rst"$'\n' # First line
        "${yellow_b} CPU$rst: USR: $blue%U$rst SYS: $blue%S$rst PER:$blue%P$rst"$'\n' # Second line
        "${yellow_b} MEM$rst: $blue%M$rst" # Third line
    )
}

bindkey -e

export EDITOR="nvim"
export VISUAL=$EDITOR
export PAGER=less

# VTE
if [ $VTE_VERSION ]; then
    source /etc/profile.d/vte.sh
fi

alias edit="$EDITOR"
alias vim=nvim

# Quote URL.
# Please note, both url-quote-magic and bracketed-paste-url-magic are loaded.

# https://archive.zhimingwang.org/blog/2015-09-21-zsh-51-and-bracketed-paste.html
autoload -Uz bracketed-paste-url-magic
zle -N bracketed-paste bracketed-paste-url-magic

# Modified from https://github.com/lilydjwg/dotzsh/blob/master/zshrc
autoload -Uz url-quote-magic
zle -N self-insert url-quote-magic
# Original name: toogle-uqm
function self-insert-switch-url-quote-magic () {
    if zle -l self-insert; then
        zle -A .self-insert self-insert && zle -M "[ZLE] Disabled url-quote-magic"
    else
        zle -N self-insert url-quote-magic && zle -M "[ZLE] Enabled url-quote-magic"
    fi
}
zle -N self-insert-switch-url-quote-magic
bindkey '^X$' self-insert-switch-url-quote-magic

# zinit.
# https://github.com/zdharma/zinit

# Load Zinit if cloned.
if [[ -f "$HOME/.zinit/bin/zinit.zsh" ]]; then
    source "$HOME/.zinit/bin/zinit.zsh"
else
    function zinit () { return 1 }
    function Zinit-Install () {
        # https://mywiki.wooledge.org/BashFAQ/081
        whence -p git >/dev/null || { print -P "%B%F{red}[Zshrc]%f Git was NOT FOUND :(%b" >&2 && return 1 }
        [[ -d "$HOME/.zinit" ]] || command mkdir -p "$HOME/.zinit"
        command git clone https://github.com/zdharma/zinit.git ~/.zinit/bin
        print -P "%B%F{green}[Zshrc]%f Zinit was installed ! :) Installing plugins...%b"
        exec zsh
    }
    print -P "%B%F{yellow}[Zshrc]%f Zinit is not installed. Install with Zinit-Install%b" >&2
    return
fi

# Zinit annexes
zinit light-mode for \
    zinit-zsh/z-a-as-monitor \
    zinit-zsh/z-a-patch-dl \
    zinit-zsh/z-a-bin-gem-node

# Basic LS_COLORS
[[ -n $DISPLAY || -n $ANDROID_ROOT ]] || eval "$(dircolors -b)"

# Extended LS_COLORS
# Uses package system to reduce code.
zinit if'[[ -n $DISPLAY || -n $ANDROID_ROOT ]]' pack for trapd00r/LS_COLORS

# Multi-word history search.
zstyle ":plugin:history-search-multi-word" reset-prompt-protect 1

# Snippets / Plugins.
zinit wait lucid light-mode for \
    OMZ::plugins/git/git.plugin.zsh \
    OMZ::plugins/command-not-found/command-not-found.plugin.zsh \
    OMZ::plugins/hitokoto/hitokoto.plugin.zsh \
    MichaelAquilina/zsh-you-should-use \
    zsh-users/zsh-history-substring-search \
    zdharma/history-search-multi-word \
    voronkovich/gitignore.plugin.zsh \
    blockf atpull'zinit creinstall -q .' zsh-users/zsh-completions \
    atload"zicompinit; zicdreplay" zdharma/fast-syntax-highlighting \
    atload"_zsh_autosuggest_start" zsh-users/zsh-autosuggestions \
    if'[[ -n $DISPLAY || -n $ANDROID_ROOT ]]' chriskempson/base16-shell \
    zdharma/zui \
    zinit-zsh/zinit-console \
    if'(( $+commands[fzf] ))' wfxr/forgit \
    as'program' pick'fasd' atclone'./fasd --init auto >! .fasd-init.zsh' src'.fasd-init.zsh' atpull'%atclone' clvv/fasd

# Git plugins.
zinit as"null" lucid light-mode for \
    sbin Fakerr/git-recall \
    sbin cloneopts paulirish/git-open \
    sbin paulirish/git-recent \
    sbin davidosomething/git-my \
    sbin atload"export _MENU_THEME=legacy" arzzen/git-quick-stats \
    sbin iwata/git-now \
    make"PREFIX=$ZPFX install" tj/git-extras \
    sbin"bin/git-dsf;bin/diff-so-fancy" zdharma/zsh-diff-so-fancy \
    sbin"git-url;git-guclone" make"GITURL_NO_CGITURL=1" zdharma/git-url

# Powerlevel10k
# Placed here to allow zinit to output things when cloning.

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

[[ -f ~/.p10k.zsh ]] && source ~/.p10k.zsh
zinit light romkatv/powerlevel10k

# Colored man pages and more.
export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_md=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[01;44;33m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[01;32m'

# You should use
YSU_MESSAGE_POSITION="after"
YSU_IGNORED_ALIASES=("zi" "zin" "zini" "zpl" "zplg" "g" "vim")

# Others
autoload -Uz zmv
autoload -Uz zargs
autoload -U edit-command-line
zle -N edit-command-line
bindkey '^x^e' edit-command-line

# Disable globs.
# These functions works better without globs.
(( $+commands[fd] )) && alias fd='noglob command fd'
(( $+commands[find] )) && alias find='noglob command find'
(( $+functions[zmv] )) && alias zmv='noglob zmv'

# Help
unalias run-help
autoload -Uz run-help
alias help=run-help

autoload -Uz run-help-git
autoload -Uz run-help-ip
autoload -Uz run-help-openssl
autoload -Uz run-help-p4
autoload -Uz run-help-sudo
autoload -Uz run-help-svk
autoload -Uz run-help-svn

# Terminal Title
autoload -Uz add-zsh-hook

function set-xterm-terminal-title () {
    printf '\e]2;%s\a' "$@"
}

function precmd-set-terminal-title () {
    set-xterm-terminal-title "${(%):-"%n@%m: %~"}"
}

function preexec-set-terminal-title () {
    set-xterm-terminal-title "${(%):-"%n@%m: "}$2"
}

if [[ "$TERM" == (screen*|xterm*|rxvt*|tmux*|putty*|konsole*|gnome*) ]]; then
    add-zsh-hook -Uz precmd precmd-set-terminal-title
    add-zsh-hook -Uz preexec preexec-set-terminal-title
fi

source "${_zdir}/terminal.zsh"
source "${_zdir}/completion.zsh"

# Remember recent dirs.
autoload -Uz chpwd_recent_dirs cdr
add-zsh-hook -Uz chpwd chpwd_recent_dirs
zstyle ':completion:*:*:cdr:*:*' menu selection

# Shell MIME.
autoload -Uz zsh-mime-setup pick-web-browser
alias -s html=pick-web-browser
sched +0 zsh-mime-setup
zstyle ':mime:*' mailcap ~/.mailcap
zstyle ':mime:*' execute-never "/run/media/${USER}/*"
zstyle ':mime:*' never-background true

# Abbreved dirs.
autoload -Uz zsh_directory_name_generic zdn_mywrapper
add-zsh-hook -Uz zsh_directory_name zdn_mywrapper
add-zsh-hook -Uz zsh_directory_name zsh_directory_name_cdr

# Git user commands.
zstyle ':completion:*:*:git:*' user-commands ${${(M)${(k)commands}:#git-*}/git-/}

if [[ -r /proc/mdstat ]]; then
    alias mdstat='cat /proc/mdstat'
fi

alias ...='cd ../../'

# From Prezto.

# BSD ls colors.
export LSCOLORS='exfxcxdxbxGxDxabagacad'

alias ls='command ls --color=auto'
alias l='ls -1A'         # Lists in one column, hidden files.
alias ll='ls -lh'        # Lists human readable sizes.
alias lr='ll -R'         # Lists human readable sizes, recursively.
alias la='ll -A'         # Lists human readable sizes, hidden files.
alias lm='la | "$PAGER"' # Lists human readable sizes, hidden files through pager.
alias lx='ll -XB'        # Lists sorted by extension (GNU only).
alias lk='ll -Sr'        # Lists sorted by size, largest last.
alias lt='ll -tr'        # Lists sorted by date, most recent last.
alias lc='lt -c'         # Lists sorted by date, most recent last, shows change time.
alias lu='lt -u'         # Lists sorted by date, most recent last, shows access time.

# Grep colors.
export GREP_COLOR='37;45'           # BSD.
export GREP_COLORS="mt=$GREP_COLOR" # GNU.

alias grep="${aliases[grep]:-grep} --color=auto"

# Load user config.
if [[ -f ~/.zshrc.local ]]; then
    source ~/.zshrc.local
fi

# vim:ft=zsh:ts=4:sw=4
